buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath files("build/libs/kotlinRunner-1.0.jar")
    }
}
plugins {
    id 'maven-publish'
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.2.51'
    id 'groovy'
}
group 'com.anatawa12.kotlinScriptRunner'
version '1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies { it ->
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile gradleApi()
    compile localGroovy()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

// for deploy
task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId group
            artifactId name
            version version

            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username property("com.anatawa12.sonatype.username")
                password property("com.anatawa12.sonatype.passeord")
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            name 'Kotlin Script Runner'
            url 'https://github.com/anatawa12/KotlinScriptRunner/'
            description 'Kotlin Script Runner is an Apache2 Licensed gradle plugin, for run kotlin script.'
            licenses {
                license {
                    name 'Apache License 2.0'
                    url 'https://github.com/anatawa12/KotlinScriptRunner/blob/master/LICENSE'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    name 'anatawa12'
                }
            }
            scm {
                url 'https://github.com/anatawa12/KotlinScriptRunner/'
                connection 'scm:git@github.com:anatawa12/KotlinScriptRunner.git'
                developerConnection 'scm:git:git@github.com:anatawa12/KotlinScriptRunner.git'
            }
            organization {
                name 'com.github.anatawa12'
                url 'https://github.com/anatawa12'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/anatawa12/KotlinScriptRunner/issues'
            }
        }
    }
}